apply plugin: 'base'
apply plugin: 'maven'
apply plugin: 'maven-publish'


publishing {
    publications {
        maven(MavenPublication) {
            groupId 'net.cosmicdynamo.web'
            artifactId 'blocs'
            version '1.0.1-SNAPSHOT'

            artifact zip
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.zipDependencies.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
    repositories {
        maven {
            // Add your credentials to ~/.gradle/gradle.properties on your local system
            // artifactoryUsername=USERNAME
            // artifactoryPassword=PASSWORD

            credentials {
                if (project.hasProperty('artifactoryUsername')) {
                    username artifactoryUsername
                    password artifactoryPassword
                }
            }

            if (publications.maven.version.endsWith('-SNAPSHOT'))
                url "http://cosmicdynamo.net/artifactory/libs-snapshot-local"
            else
                url "http://cosmicdynamo.net/artifactory/libs-release-local"
        }
    }
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

task zip(type: Zip) {
    from 'src'
    exclude '**/dojo'
}

assemble.dependsOn(zip)

repositories {
    mavenLocal ()
    maven {
        url "http://cosmicdynamo.net/artifactory/libs-release"
    }
    maven {
        url "http://cosmicdynamo.net/artifactory/libs-snapshot"
    }
    maven {
        url "http://cosmicdynamo.net/artifactory/repo1"
    }
}

configurations {
    dojo
    cosmicdynamo
}

dependencies {
    dojo "org.dojotoolkit:dojo:1.10.4"
    cosmicdynamo "net.cosmicdynamo.web:pollyfill:1.0.1-SNAPSHOT"
}

task dependencies(type: Copy) {
    configurations.dojo.files.each { file ->
        from zipTree(file)
        into "src/scripts/"
    }
    configurations.cosmicdynamo.files.each { file ->
        from zipTree(file)
        into "src/"
    }
}
